buildscript {
  repositories {
    jcenter()
    google()
  }
  dependencies {
    classpath "com.android.tools.build:gradle:$gradlePluginVersion"
  }
}

apply plugin: "com.android.application"

final def useGit = System.getenv("CI") != null && "which git".execute().waitFor() == 0
android {
  buildToolsVersion _buildToolsVersion
  compileSdkVersion _compileSdkVersion.toInteger()
  buildTypes {
    debug {
      matchingFallbacks = ["release"]
    }
  }
  defaultConfig {
    applicationId "onautoscrolledtoview.demo"
    minSdkVersion 17
    targetSdkVersion 30
    versionCode useGit ? "git rev-list --count HEAD".execute().text.toInteger() : Integer.MAX_VALUE
    versionName useGit ? "git describe --abbrev=0".execute().text.trim() : String.valueOf(versionCode)
    resConfigs "en"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  variantFilter {
    if (it.buildType.name == "release") {
      it.ignore = true
    }
  }
}

lint.onlyIf {
  false
}

repositories {
  google()
  jcenter()
}

dependencies {
  implementation "androidx.recyclerview:recyclerview:1.1.0"
  androidTestImplementation "androidx.test:rules:1.2.0"
  androidTestImplementation "androidx.test:runner:1.2.0"
  androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
  // Replace by "com.github.stoyicker.onautoscrolledtoview:library:$version" in your build.gradle
  androidTestImplementation project(path: ":library")
}
