import java.nio.file.Paths

buildscript {
  repositories {
    jcenter()
    google()
  }
  dependencies {
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5"
    classpath "com.android.tools.build:gradle:$gradlePluginVersion"
  }
}

apply plugin: "com.android.library"

final def useGit = System.getenv("CI") != null && "which git".execute().waitFor() == 0
android {
  buildToolsVersion _buildToolsVersion
  compileSdkVersion _compileSdkVersion.toInteger()
  buildTypes {
    release {
      minifyEnabled false
      consumerProguardFiles "proguard/rules.pro"
    }
  }
  defaultConfig {
    minSdkVersion 17
    targetSdkVersion 30
    versionCode useGit ? "git rev-list --count HEAD".execute().text.toInteger() : Integer.MAX_VALUE
    versionName useGit ? "git describe --abbrev=0".execute().text.trim() : String.valueOf(versionCode)
    resConfigs "en"
  }
  variantFilter {
    if (it.buildType.name == "debug") {
      it.ignore = true
    }
  }
}

repositories {
  google()
  jcenter()
}

dependencies {
  compileOnly "org.projectlombok:lombok:1.18.12"
  annotationProcessor "org.projectlombok:lombok:1.18.12"
  implementation "androidx.test.espresso:espresso-core:$espressoVersion"
}

gradle.projectsEvaluated {
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all"
  }
}

if (System.getenv("CI") != null && "which git".execute().waitFor() == 0) {
  apply from: Paths.get(projectDir.absolutePath, 'publish.gradle')
}
